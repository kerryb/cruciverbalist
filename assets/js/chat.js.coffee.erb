window.setupChat = (conversationID) ->
  conversationURL = "/conversation/#{conversationID}/messages"
  viewModel = new ChatViewModel conversationURL
  ko.applyBindings viewModel
  setupPusher viewModel, "chat-#{conversationID}"
  loadMessages viewModel, conversationURL
  $("#new-message").focus()

class Message
  constructor: (attrs) ->
    @username = attrs.username
    @content = attrs.content
    @type = attrs.type

class ChatViewModel
  constructor: (@conversationURL) ->

  messages: ko.observableArray()
  newMessage: ko.observable ""

  keyPressed: (data, event) =>
    return true unless event.charCode is 13
    unless @newMessage() is ""
      $.post @conversationURL, event.target.value
    $("#conversation").scrollTop $("#conversation").height()
    @newMessage ""

setupPusher = (viewModel, channel) ->
  pusher = new Pusher "<%= Pusher.key %>"
  channel = pusher.subscribe channel
  channel.bind "new-chat-message", (data) ->
    viewModel.messages.push new Message(JSON.parse(data.message))

loadMessages = (viewModel, url) ->
  $.getJSON url, (data) ->
    viewModel.messages.push new Message(message) for message in data
