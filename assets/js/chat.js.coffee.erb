window.setupChat = (crosswordID) ->
  crosswordURL = "/crossword/#{crosswordID}/messages"
  viewModel = new ChatViewModel crosswordURL
  ko.applyBindings viewModel
  setupPusher viewModel, "chat-#{crosswordID}"
  loadMessages viewModel, crosswordURL
  $("#new-message").focus()

class Message
  constructor: (attrs) ->
    @username = attrs.username
    @content = attrs.content
    @type = attrs.type

class ChatViewModel
  constructor: (@crosswordURL) ->

  messages: ko.observableArray()
  newMessage: ko.observable ""

  keyPressed: (data, event) =>
    return true unless event.charCode is 13
    unless @newMessage() is ""
      $.post @crosswordURL, event.target.value
    $("#conversation").scrollTop $("#conversation").height()
    @newMessage ""

setupPusher = (viewModel, channel) ->
  pusher = new Pusher "<%= Pusher.key %>"
  channel = pusher.subscribe channel
  channel.bind "new-chat-message", (data) ->
    viewModel.messages.push new Message(JSON.parse(data.message))

loadMessages = (viewModel, url) ->
  $.getJSON url, (data) ->
    viewModel.messages.push new Message(message) for message in data
