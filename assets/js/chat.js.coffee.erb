window.setupChat = (crosswordID) ->
  chatURL = "/crossword/#{crosswordID}/messages"
  viewModel = new ChatViewModel chatURL
  ko.applyBindings viewModel
  setupPusher viewModel, "chat-#{crosswordID}"
  loadMessages viewModel, chatURL
  $("#new-message").focus()

class Message
  constructor: (attrs) ->
    @username = attrs.username
    @content = attrs.content
    @type = attrs.type

class ChatViewModel
  constructor: (@chatURL) ->

  messages: ko.observableArray()
  newMessage: ko.observable ""

  keyPressed: (data, event) =>
    return true unless event.charCode is 13
    unless @newMessage() is ""
      $.post @chatURL, event.target.value
    $("#conversation").scrollTop $("#conversation").height()
    @newMessage ""

setupPusher = (viewModel, channel) ->
  pusher = new Pusher "<%= Pusher.key %>"
  channel = pusher.subscribe channel
  channel.bind "new-chat-message", (data) ->
    viewModel.messages.push new Message(JSON.parse(data.message))

loadMessages = (viewModel, url) ->
  $.getJSON url, (data) ->
    viewModel.messages.push new Message(message) for message in data
